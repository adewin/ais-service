buildscript {
    ext {
        scala_compat_version = "2.11"
        scala_version = "$scala_compat_version.12"
        spark_version = "2.4.1"
        geotrellis_version = "2.2.0"
    }
}

plugins {
    id "scala"
    id "application"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

group "uk.gov.ukho"
version "0.8-SNAPSHOT"

repositories {
    maven {
        url "https://repo.boundlessgeo.com/main/"
    }
    maven {
        url "http://maven.geomajas.org/"
    }
    mavenCentral()
}

application {
    mainClassName = "uk.gov.ukho.ais.rasters.AisToRaster"
    applicationDefaultJvmArgs = ["-Dspark.master=local[*]"]
}

shadowJar {
    zip64 = true

    manifest {
        attributes "Main-Class": application.mainClassName
    }

    minimize()
    dependencies {
        exclude(dependency("org.apache.spark:.*"))
    }
}

dependencies {
    implementation "org.scala-lang:scala-library:$scala_version"

    implementation "org.locationtech.geotrellis:geotrellis-spark_$scala_compat_version:$geotrellis_version"
    implementation "org.locationtech.geotrellis:geotrellis-shapefile_$scala_compat_version:$geotrellis_version"

    implementation "org.apache.spark:spark-core_$scala_compat_version:$spark_version"
    implementation "org.apache.spark:spark-sql_$scala_compat_version:$spark_version"
}
